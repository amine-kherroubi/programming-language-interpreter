program ::= block EOF
block ::= "{" (statement NEWLINE)* give_statement? "}"
statement ::= assignment | variable_declaration | constant_declaration | procedure_unit | unit_use | give_statement

assignment ::= IDENTIFIER "=" assignable
assignable ::= expression | procedure_unit

variable_declaration ::= "let" same_type_group (";" same_type_group)*
constant_declaration ::= "keep" same_type_group (";" same_type_group)*
same_type_group ::= identifier_group ":" type ("=" expression_group)?
identifier_group ::= IDENTIFIER ("," IDENTIFIER)*
expression_group ::= expression ("," expression)*

unit_use ::= IDENTIFIER "(" args? ")"
args ::= expression ("," expression)*

give_statement ::= "give" expression?

expression ::= power_expression (("+" | "-") power_expression)*
power_expression ::= term ("**" power_expression)?
term ::= factor (("*" | "/" | "//" | "%") factor)*
factor ::= literal | IDENTIFIER | "(" expression ")" | unit_use | expression_unit
literal ::= WHOLE_LITERAL | REAL_LITERAL | TEXT_LITERAL | TRUTH_LITERAL

procedure_unit ::= ("[" parameters "]")? block
expression_unit ::= "[" parameters? "]" return_type block
parameters ::= parameter ("," parameter)*
parameter ::= IDENTIFIER ":" type
return_type ::= "->" type

type ::= WHOLE_TYPE | REAL_TYPE | TEXT_TYPE | TRUTH_TYPE | UNIT_TYPE

IDENTIFIER ::= [a-zA-Z_][a-zA-Z0-9_]*
WHOLE_LITERAL ::= [0-9]+
REAL_LITERAL ::= [0-9]+"."[0-9]+
TEXT_LITERAL ::= '"' .* '"'
TRUTH_LITERAL ::= "true" | "false"
NEWLINE ::= "\n"
EOF ::= end-of-input