program ::= block
block ::= "{" statement_list "}"
statement_list ::= (statement NEWLINE)*
statement ::= assignment | variable_declaration | constant_declaration | function_declaration | procedure_declaration | procedure_call | give_statement

assignment ::= IDENTIFIER "=" expression

variable_declaration ::= "let" type identifier_list ("=" expression_list)?
constant_declaration ::= "keep" type identifier_list "=" expression_list
identifier_list ::= IDENTIFIER ("," IDENTIFIER)*
expression_list ::= expression ("," expression)*

function_declaration ::= "func" IDENTIFIER "(" parameter_list? ")" return_type block
procedure_declaration ::= "proc" IDENTIFIER "(" parameter_list? ")" block
parameter_list ::= parameter ("," parameter)*
parameter ::= IDENTIFIER ":" type
return_type ::= "->" type

type ::= "int" | "float" | "string" | "bool"

function_call ::= IDENTIFIER "(" argument_list? ")"
procedure_call ::= "exec" IDENTIFIER "(" argument_list? ")"
argument_list ::= expression ("," expression)*

give_statement ::= "give" expression?

expression ::= additive_expression
additive_expression ::= multiplicative_expression (("+" | "-") multiplicative_expression)*
multiplicative_expression ::= power_expression (("*" | "/" | "//" | "%") power_expression)*
power_expression ::= unary_expression ("**" unary_expression)?
unary_expression ::= ("+" | "-")? primary_expression
primary_expression ::= literal | IDENTIFIER | "(" expression ")" | function_call

literal ::= INT_LITERAL | FLOAT_LITERAL | STRING_LITERAL | BOOL_LITERAL

IDENTIFIER ::= [a-zA-Z_][a-zA-Z0-9_]*
INT_LITERAL ::= [0-9]+
FLOAT_LITERAL ::= [0-9]*"."[0-9]+
STRING_LITERAL ::= ('"' [^"\n]* '"') | ("'" [^'\n]* "'")
BOOL_LITERAL ::= "true" | "false"

NEWLINE ::= "\n"
EOF ::= end-of-input
