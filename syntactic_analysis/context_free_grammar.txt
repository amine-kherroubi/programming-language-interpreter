program ::= block
block ::= "{" (statement NEWLINE)* "}"
statement ::= variable_declaration | constant_declaration | function_declaration | procedure_declaration | procedure_call | assignment_statement | give_statement | show_statement | if_statement | while_statement | skip_statement | stop_statement

# Variable and constant declarations
variable_declaration ::= "let" type identifier_list ("=" expression_list)?
constant_declaration ::= "keep" type identifier_list "=" expression_list
identifier_list ::= IDENTIFIER ("," IDENTIFIER)*
expression_list ::= expression ("," expression)*

# Function and procedure declarations  
function_declaration ::= "func" IDENTIFIER "(" parameter_list? ")" return_type block
procedure_declaration ::= "proc" IDENTIFIER "(" parameter_list? ")" block
parameter_list ::= parameter ("," parameter)*
parameter ::= type IDENTIFIER
return_type ::= "->" type
type ::= "int" | "float" | "string" | "bool"

# Function and procedure calls
function_call ::= IDENTIFIER "(" argument_list? ")"
procedure_call ::= "exec" IDENTIFIER "(" argument_list? ")"
argument_list ::= expression ("," expression)*

# Statements
assignment_statement ::= IDENTIFIER "=" expression
give_statement ::= "give" expression?
show_statement ::= "show" expression
if_statement ::= "if" boolean_expression block elifs? else?
elifs ::= ("elif" boolean_expression block)+
else ::= "else" block
while_statement ::= "while" boolean_expression block
skip_statement ::= "skip"
stop_statement ::= "stop"

# Expressions
expression ::= boolean_expression | arithmetic_expression

# Boolean expressions (for conditions and comparisons)
boolean_expression ::= logical_or_expression
logical_or_expression ::= logical_and_expression ("or" logical_and_expression)*
logical_and_expression ::= logical_not_expression ("and" logical_not_expression)*
logical_not_expression ::= "not"? primary_boolean_expression
primary_boolean_expression ::= comparison_expression | boolean_literal | "(" boolean_expression ")"

# Comparisons
comparison_expression ::= arithmetic_expression comparison_operator arithmetic_expression
comparison_operator ::= "==" | "!=" | "<" | ">" | "<=" | ">="

# Arithmetic expressions
arithmetic_expression ::= additive_expression
additive_expression ::= multiplicative_expression (("+" | "-") multiplicative_expression)*
multiplicative_expression ::= power_expression (("*" | "/" | "//" | "%") power_expression)*
power_expression ::= unary_expression ("**" unary_expression)?
unary_expression ::= ("+" | "-")? primary_expression
primary_expression ::= literal | IDENTIFIER | "(" arithmetic_expression ")" | function_call

# Literals
literal ::= INT_LITERAL | FLOAT_LITERAL | STRING_LITERAL
boolean_literal ::= BOOL_LITERAL

# Terminals
IDENTIFIER ::= [a-zA-Z_][a-zA-Z0-9_]*
INT_LITERAL ::= [0-9]+
FLOAT_LITERAL ::= [0-9]*"."[0-9]+
STRING_LITERAL ::= ('"' [^"\n]* '"') | ("'" [^'\n]* "'")
BOOL_LITERAL ::= "true" | "false"
NEWLINE ::= "\n"
EOF ::= end-of-input